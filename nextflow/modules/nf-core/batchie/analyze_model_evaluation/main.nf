process ANALYZE_MODEL_EVALUATION {
    tag "$meta.id"
    label 'process_single'
    container "${ workflow.containerEngine == 'singularity' && !task.ext.singularity_pull_docker_container ?
        'docker://jeffquinnmsk/batchie:latest' :
        'docker.io/jeffquinnmsk/batchie:latest' }"

    input:
    tuple val(meta), path(training_data), path(thetas), path(model_evaluation)

    output:
    tuple val(meta), path("${prefix}/model_evaluation_analysis"), emit: results
    path  "versions.yml"                , emit: versions


    when:
    task.ext.when == null || task.ext.when

    script:
    prefix = task.ext.prefix ?: "${meta.id}"
    def args = task.ext.args ?: ""
    """
    mkdir -p "${prefix}"
    analyze_model_evaluation --screen ${training_data} \
        --thetas ${thetas} \
        --model-evaluation ${model_evaluation} \
        --output-dir "${prefix}/model_evaluation_analysis" \
        ${args}

    cat <<-END_VERSIONS > versions.yml
    "${task.process}":
        batchie: \$( python -c 'from importlib.metadata import version;print(version("batchie"))' )
    END_VERSIONS
    """
}
